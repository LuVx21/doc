---
title: JVM3:方法调用
date: 2017-11-06
tags:
- Java
- JVM
---

## 总结

### 方法重载

Java文件中, 同名, 同参数的方法不可同时存在, 会编译出错.

![1559614282685](https://gitee.com/LuVx/img/raw/master/1559613985692.png)

重载方法的选择过程:

1. 在不考虑对基本类型自动装拆箱(auto-boxing, auto-unboxing), 以及可变长参数的情况下选取重载方法;
2. 如果在第 1 个阶段中没有找到适配的方法, 那么在允许自动装拆箱, 但不允许可变长参数的情况下选取重载方法;
3. 如果在第 2 个阶段中没有找到适配的方法, 那么在允许自动装拆箱以及可变长参数的情况下选取重载方法.

**两个`invoke()`方法都是可变长的, 即都在第3阶段找到, 又是怎么区分的? 优先选择可变参数靠后的方法?**

同一个阶段中找到了多个适配的方法, 那么它会在其中选择一个最为贴切的, 而决定贴切程度的一个关键就是形式参数类型的继承关系.即优先选择与参数类型匹配的子类.

小知识: 这个限制可以通过字节码工具绕开.
也就是说, 在编译完成之后, 我们可以再向 class 文件中添加方法名和参数类型相同, 而返回类型不同的方法.
当这种包括多个方法名相同, 参数类型相同, 而返回类型不同的方法的类, 出现在 Java 编译器的用户类路径上时, 它是怎么确定需要调用哪个方法的呢?
当前版本的 Java 编译器会直接选取第一个方法名以及参数类型匹配的方法.并且, 它会根据所选取方法的返回类型来决定可不可以通过编译, 以及需不需要进行值转换等.

```Java
public class MethodTest {
    public String method1(String str) {
        String mtdName = Thread.currentThread().getStackTrace()[1].getMethodName();
        System.out.println("invoke " + mtdName + " return String");
        return "";
    }

    public int method2(String str) {
        String mtdName = Thread.currentThread().getStackTrace()[1].getMethodName();
        System.out.println("invoke " + mtdName + " return int");
        return 1;
    }

    public static void main(String[] args) {
        MethodTest javacTestOverload = new MethodTest();
        String str = javacTestOverload.method1("Test");
        int i = javacTestOverload.method2("Test");
    }
}
```
编译后修改class文件将`method2`修改为`method1`后, 反编译的结果如下:

![image-20190603224423190](https://gitee.com/LuVx/img/raw/master/20190603224423190.png)

运行:

![image-20190603224801064](https://gitee.com/LuVx/img/raw/master/20190603224801064.png)

Class文件中同名, 同参数, 不同返回值的方法可以正常运行.
那为什么Java文件中不可以呢: 因为Java语言规范的规定, 所以编译时会出现错误.
那为什么Class文件可以跑呢: 因为Java虚拟机规范与Java规范不同, 两者是分开定义的.

为什么方法重载不可以根据返回类型区分?

浅显的说法:

一个方法被调用时, 不一定就会将返回值赋给一个指定类型的变量, 从而无从知道返回类型.

书本上的说法:

![1559551568547](https://gitee.com/LuVx/img/raw/master/1559551568547.png)

![1559633889938](https://gitee.com/LuVx/img/raw/master/1559633889938.png)

> 不建议重载不定长参数方法

### 方法重写

重写比重载要求更严格, 前者要求方法名, 参数类型, 返回值类型可兼容, 后者只要求方法名相同, 参数类型可不同, 返回值类型可不同

方法的重载和重写都是实现多态的方式, 区别在于前者实现的是编译时的多态性, 而后者实现的是运行时的多态性.
重载发生在一个类中, 同名的方法如果有不同的参数列表(参数类型不同, 参数个数不同或者二者都不同)则视为重载;

重写发生在子类与父类之间, 重写要求子类被重写方法与父类被重写方法有相同的参数列表, 有兼容的返回类型, 比父类被重写方法更好访问, 不能比父类被重写方法声明更多的异常(里氏代换原则).
重载对返回类型没有特殊的要求, 不能根据返回类型进行区分.

### 静态绑定&&动态绑定

重载: 静态绑定(static binding), 解析时即可确定方法的情况
重写: 动态绑定(dynamic binding), 必须运行时候才能确定方法的情况

**为什么叫做静态绑定,动态绑定?**

静态绑定指的是在解析时便能够直接识别目标方法的情况
动态绑定则指的是需要在运行过程中根据调用者的动态类型来识别目标方法的情况

JVM层面则可以认为java指令在调用方法时, 符号引用和实际引用之间的关联类型, 下一小节中说到:
静态绑定的方法调用而言, 实际引用为目标方法的指针, 这个指针是静态的;
对于需要动态绑定的方法调用而言, 实际引用为辅助动态绑定的信息, 这些信息是动态的;

### 调用指令的符号引用

在编译过程中, 我们并不知道目标方法的具体内存地址. 因此, Java 编译器会暂时用符号引用来表示该目标方法.这一点可以简单的类比为占位符.
符号引用包括目标方法所在的类或接口的名字, 以及目标方法的方法名和方法描述符.
每个class文件都有一个常量池, 主要是关于类, 方法, 接口等中的常量, 也包括字符串常量和符号引用.
方法的符号引用是唯一标识一个方法的信息结构体, 包含类名, 方法名和方法描述符, 方法描述符又包含返回值, 函数名和参数列表.
根据目标方法是否为接口方法, 这些引用可分为接口符号引用(`InterfaceMethodref`)和非接口符号引用(`Methodref`)

1. `invokestatic`: 调用静态方法
2. `invokespecial`: 调用实例构造函数`<init>`方法, 私有方法和父类方法(super(),super.method()).
3. `invokevirtual`: 调用所有的虚方法(静态方法, 私有方法, 实例构造函数, 父类方法, final方法都是非虚方法).
4. `invokeinterface`: 调用接口方法, 会在运行时期再确定一个实现此接口的对象.
5. `invokedynamic`: 现在运行时期动态解析出调用点限定符所引用的方法, 然后再执行该方法, 在此之前的4条指令, 分派逻辑都是固化在虚拟机里面的, 而invokedynamic指令的分派逻辑是由用户所设定的引导方法决定的.

类的解析期间, 会将一部分符号引用转为直接引用(包括静态方法, 私有方法, 构造函数, 父类方法), 直接引用就是一个指针或偏移量, 可以让JVM快速定位到具体要调用的方法

`invokestatic`和`invokespecial`指令调用的方法都可以在解析阶段中确定唯一的调用版本, 符合这个条件的就是上面所说解析阶段完成符号替换的方法.

`invokevirtual`和`invokeinterface`指令调用的方法是在运行时确定具体的方法地址, 接口方法和实例对象公有方法可以用这两个指令来调用.
通过查看字节码可知, 每一条方法调用指令后面都带一个类似`#1`这样的值, 通过这个值可以从常量池中获取到方法的符号引用

当JVM遇到`invokevirtual`或`invokeinterface`时, 需要运行时根据方法的符号引用查找到方法地址.具体过程如下:

1. 在方法调用指令之前, 需要将对象的引用压入操作数栈
2. 在执行方法调用时, 找到操作数栈顶的第一个元素所指向的对象实际类型, 记作C
3. 在类型C中找到与常量池中的描述符和方法名称都相符的方法, 并校验访问权限.如果找到该方法并通过校验, 则返回这个方法的引用;
4. 否则, 按照继承关系往上查找方法并校验访问权限;
5. 如果始终没找到方法, 则抛出java.lang.AbstractMethodError异常;
可以看到, JVM是通过继承关系从子类往上查找的对应的方法的.

```Java
interface Customer {
    boolean isVIP();
}
class Merchant {
    public double afterPreferential(double price, Customer customer) {
        return price * 0.8d;
    }
}
class BedMerchant extends Merchant {
    @Override
    public double afterPreferential(double price, Customer customer) {
        if (customer.isVIP()) {
            return price * bedPrice();
        } else {
            return super.afterPreferential(price, customer);
        }
    }

    public static double bedPrice() {
        return new Random()
                .nextDouble()
                + 0.8d;
    }
}
```

```Java
interface org.luvx.demo.Customer
  minor version: 0
  major version: 52
  flags: ACC_INTERFACE, ACC_ABSTRACT
Constant pool:
  #1 = Class              #7              // org/luvx/demo/Customer
  #2 = Class              #8              // java/lang/Object
  #3 = Utf8               isVIP
  #4 = Utf8               ()Z
  #5 = Utf8               SourceFile
  #6 = Utf8               Customer.java
  #7 = Utf8               org/luvx/demo/Customer
  #8 = Utf8               java/lang/Object
{
  public abstract boolean isVIP();
    descriptor: ()Z
    flags: ACC_PUBLIC, ACC_ABSTRACT
}

class org.luvx.demo.Merchant
  minor version: 0
  major version: 52
  flags: ACC_SUPER
Constant pool:
   #1 = Methodref          #5.#21         // java/lang/Object."<init>":()V
   #2 = Double             0.8d
   #4 = Class              #22            // org/luvx/demo/Merchant
   #5 = Class              #23            // java/lang/Object
   #6 = Utf8               <init>
   #7 = Utf8               ()V
   #8 = Utf8               Code
   #9 = Utf8               LineNumberTable
  #10 = Utf8               LocalVariableTable
  #11 = Utf8               this
  #12 = Utf8               Lorg/luvx/demo/Merchant;
  #13 = Utf8               afterPreferential
  #14 = Utf8               (DLorg/luvx/demo/Customer;)D
  #15 = Utf8               price
  #16 = Utf8               D
  #17 = Utf8               customer
  #18 = Utf8               Lorg/luvx/demo/Customer;
  #19 = Utf8               SourceFile
  #20 = Utf8               Merchant.java
  #21 = NameAndType        #6:#7          // "<init>":()V
  #22 = Utf8               org/luvx/demo/Merchant
  #23 = Utf8               java/lang/Object
{
  org.luvx.demo.Merchant();
    descriptor: ()V
    flags:
    Code:
      stack=1, locals=1, args_size=1
         0: aload_0
         1: invokespecial #1                  // Method java/lang/Object."<init>":()V
         4: return
      LineNumberTable:
        line 9: 0
      LocalVariableTable:
        Start  Length  Slot  Name   Signature
            0       5     0  this   Lorg/luvx/demo/Merchant;

  public double afterPreferential(double, org.luvx.demo.Customer);
    descriptor: (DLorg/luvx/demo/Customer;)D
    flags: ACC_PUBLIC
    Code:
      stack=4, locals=4, args_size=3
         0: dload_1
         1: ldc2_w        #2                  // double 0.8d
         4: dmul
         5: dreturn
      LineNumberTable:
        line 11: 0
      LocalVariableTable:
        Start  Length  Slot  Name   Signature
            0       6     0  this   Lorg/luvx/demo/Merchant;
            0       6     1 price   D
            0       6     3 customer   Lorg/luvx/demo/Customer;
}

class org.luvx.demo.BedMerchant extends org.luvx.demo.Merchant
  minor version: 0
  major version: 52
  flags: ACC_SUPER
Constant pool:
   #1 = Methodref          #11.#30        // org/luvx/demo/Merchant."<init>":()V
   #2 = InterfaceMethodref #31.#32        // org/luvx/demo/Customer.isVIP:()Z
   #3 = Methodref          #10.#33        // org/luvx/demo/BedMerchant.bedPrice:()D
   #4 = Methodref          #11.#34        // org/luvx/demo/Merchant.afterPreferential:(DLorg/luvx/demo/Customer;)D
   #5 = Class              #35            // java/util/Random
   #6 = Methodref          #5.#30         // java/util/Random."<init>":()V
   #7 = Methodref          #5.#36         // java/util/Random.nextDouble:()D
   #8 = Double             0.8d
  #10 = Class              #37            // org/luvx/demo/BedMerchant
  #11 = Class              #38            // org/luvx/demo/Merchant
  #12 = Utf8               <init>
  #13 = Utf8               ()V
  #14 = Utf8               Code
  #15 = Utf8               LineNumberTable
  #16 = Utf8               LocalVariableTable
  #17 = Utf8               this
  #18 = Utf8               Lorg/luvx/demo/BedMerchant;
  #19 = Utf8               afterPreferential
  #20 = Utf8               (DLorg/luvx/demo/Customer;)D
  #21 = Utf8               price
  #22 = Utf8               D
  #23 = Utf8               customer
  #24 = Utf8               Lorg/luvx/demo/Customer;
  #25 = Utf8               StackMapTable
  #26 = Utf8               bedPrice
  #27 = Utf8               ()D
  #28 = Utf8               SourceFile
  #29 = Utf8               BedMerchant.java
  #30 = NameAndType        #12:#13        // "<init>":()V
  #31 = Class              #39            // org/luvx/demo/Customer
  #32 = NameAndType        #40:#41        // isVIP:()Z
  #33 = NameAndType        #26:#27        // bedPrice:()D
  #34 = NameAndType        #19:#20        // afterPreferential:(DLorg/luvx/demo/Customer;)D
  #35 = Utf8               java/util/Random
  #36 = NameAndType        #42:#27        // nextDouble:()D
  #37 = Utf8               org/luvx/demo/BedMerchant
  #38 = Utf8               org/luvx/demo/Merchant
  #39 = Utf8               org/luvx/demo/Customer
  #40 = Utf8               isVIP
  #41 = Utf8               ()Z
  #42 = Utf8               nextDouble
{
  org.luvx.demo.BedMerchant();
    descriptor: ()V
    flags:
    Code:
      stack=1, locals=1, args_size=1
         0: aload_0
         1: invokespecial #1                  // Method org/luvx/demo/Merchant."<init>":()V
         4: return
      LineNumberTable:
        line 11: 0
      LocalVariableTable:
        Start  Length  Slot  Name   Signature
            0       5     0  this   Lorg/luvx/demo/BedMerchant;

  public double afterPreferential(double, org.luvx.demo.Customer);
    descriptor: (DLorg/luvx/demo/Customer;)D
    flags: ACC_PUBLIC
    Code:
      stack=4, locals=4, args_size=3
         0: aload_3
         1: invokeinterface #2, 1            // InterfaceMethod org/luvx/demo/Customer.isVIP:()Z
         6: ifeq          15
         9: dload_1
        10: invokestatic  #3                  // Method bedPrice:()D
        13: dmul
        14: dreturn
        15: aload_0
        16: dload_1
        17: aload_3
        18: invokespecial #4                  // Method org/luvx/demo/Merchant.afterPreferential:(DLorg/luvx/demo/Customer;)D
        21: dreturn
      LineNumberTable:
        line 14: 0
        line 15: 9
        line 17: 15
      LocalVariableTable:
        Start  Length  Slot  Name   Signature
            0      22     0  this   Lorg/luvx/demo/BedMerchant;
            0      22     1 price   D
            0      22     3 customer   Lorg/luvx/demo/Customer;
      StackMapTable: number_of_entries = 1
        frame_type = 15 /* same */

  public static double bedPrice();
    descriptor: ()D
    flags: ACC_PUBLIC, ACC_STATIC
    Code:
      stack=4, locals=0, args_size=0
         0: new           #5                  // class java/util/Random
         3: dup
         4: invokespecial #6                  // Method java/util/Random."<init>":()V
         7: invokevirtual #7                  // Method java/util/Random.nextDouble:()D
        10: ldc2_w        #8                  // double 0.8d
        13: dadd
        14: dreturn
      LineNumberTable:
        line 22: 0
        line 23: 7
        line 22: 14
}
```

**符号引用会被解析成实际引用的过程**

对于非接口符号引用, 假定该符号引用所指向的类为 C, 则 Java 虚拟机会按照如下步骤进行查找:

1. 在 C 中查找符合名字及描述符的方法.
2. 如果没有找到, 在 C 的父类中继续搜索, 直至 Object 类.
3. 如果没有找到, 在 C 所直接实现或间接实现的接口中搜索, 这一步搜索得到的目标方法必须是非私有, 非静态的.并且, 如果目标方法在间接实现的接口中, 则需满足 C 与该接口之间没有其他符合条件的目标方法.如果有多个符合条件的目标方法, 则任意返回其中一个.

通过以上过程, 就可以理解`父类静态方法可以通过子类来调用`, `子类的静态方法会隐藏父类中的同名, 同描述符的静态方法`的原因

> 不建议在父子类中使用相同属性名, 静态方法名

对于接口符号引用, 假定该符号引用所指向的接口为 `I`, 则 Java 虚拟机会按照如下步骤进行查找:

1. 在 `I` 中查找符合名字及描述符的方法.
2. 如果没有找到, 在 Object 类中的公有实例方法中搜索.
3. 如果没有找到, 则在 `I` 的父接口中搜索. 这一步的搜索结果的要求与非接口符号引用步骤 3 的要求一致.

### 桥接方法

Java 的重写与 Java 虚拟机中的重写并不一致, 但是编译器会通过生成桥接方法来弥补
生成的桥接方法会有两个标记:
* `acc_bridge`: 是由编译器生成, 并且不会在源代码中出现
* `acc_synthetic`: 由编译生成的桥接方法

![1559563955114](https://gitee.com/LuVx/img/raw/master/1559563955114.png)

**什么时候生成**

* 子类在继承(或实现)一个父类(或接口)的泛型方法时, 在子类中明确指定了泛型类型, 那么在编译时编译器会自动生成桥接方法
* 方法重写时候, 方法签名发生改变

```Java
public class Node<T> {
    public T data;

    public Node(T data) { this.data = data; }

    public void setData(T data) {
        System.out.println("Node.setData");
        this.data = data;
    }
}
public class MyNode extends Node<Integer> {
    public MyNode(Integer data) {
        super(data);
    }

    @Override
    public void setData(Integer data) {
        System.out.println("MyNode.setData");
        super.setData(data);
    }
}
```
![1559628270119](https://gitee.com/LuVx/img/raw/master/1559628270119.png)

子类内部其实有一个桥接方法:

```Java
public void setData(Object data) {
    setData((Integer) data);
}
```

测试:
```Java
public static void method3() {
    Node mn = new MyNode(5);
    /// 调用实际方法
    mn.setData(new Integer(5));
    /// 调用桥接方法
    mn.setData(new Object());// 异常: ClassCastException, 发生点在桥接方法强制转换时
}
```
声明父类类型的变量时没有指定类型, 那么传入任何类型都行, 如上.

如果声明时即指定类型, 则会把运行时异常提前到编译时:
```Java
public static void method4() {
    Node<Integer> mn = new MyNode(5);
    /// 调用实际方法
    mn.setData(new Integer(5));
    /// 调用桥接方法
    mn.setData(new Object()); // 直接出错: setData (java.lang.Integer) in Node cannot be applied to (java.lang.Object)
}
```
> 如果`MyNode mn = new MyNode(5);`这样声明, 也会将异常提前

**为什么生成**

泛型擦除

上一个例子中父类的字节码:
```
public void setData(T);
  descriptor: (Ljava/lang/Object;)V
  flags: ACC_PUBLIC
  Code:
    stack=2, locals=2, args_size=2
        0: getstatic     #3                  // Field java/lang/System.out:Ljava/io/PrintStream;
        3: ldc           #4                  // String Node.setData
        5: invokevirtual #5                  // Method java/io/PrintStream.println:(Ljava/lang/String;)V
        8: aload_0
        9: aload_1
      10: putfield      #2                  // Field data:Ljava/lang/Object;
      13: return
    LineNumberTable:
      line 15: 0
      line 16: 8
      line 17: 13
    LocalVariableTable:
      Start  Length  Slot  Name   Signature
          0      14     0  this   Lorg/luvx/demo4/Node;
          0      14     1  data   Ljava/lang/Object;
    LocalVariableTypeTable:
      Start  Length  Slot  Name   Signature
          0      14     0  this   Lorg/luvx/demo4/Node<TT;>;
          0      14     1  data   TT;
  Signature: #21                          // (TT;)V
```

把`T`换成了`Object`, 如果没有桥接方法, 子类重写这个方法时, 就只能指定参数类型为`Object`;
桥接方法的存在, 将子类特定类型参数的方法和父类`Object`类型的方法关联起来.

通过泛型的分析, 方法重写时`签名不一致 -> 生成桥接方法`的原因也就可以理解了,

> 可以通过手段获取到桥接方法:`Method.isBridge()`, `org.springframework.core.BridgeMethodResolver`

## 扩展

### 内存

| 区域         | 作用                                              | 共享性 | 存储内容                                              |
| ------------ | ------------------------------------------------- | ------ | ----------------------------------------------------- |
| 堆内存       | 存放对象实例,可以细分为新生代和老年代             | √      | new出来的对象(属性,方法的地址(指向方法区))            |
| 方法区       | 内有运行时常量池,也有人称之为永久代               | √      | 常量,static变量,类信息(属性,方法)                     |
| 运行时常量池 | 方法区的一部分                                    | √      | 运行时常量池(各种字面量和符号引用)                    |
| 程序计数器   | 比较小的内存区域,指示当前线程所执行的字节码的位置 | ×      | 正在执行的VM字节码指令地址(Java方法,native方法时为空) |
| VM栈         | 记录方法调用                                      | ×      | 局部变量表,对象的引用指针                             |
| 本地方法栈   | 执行Native方法时使用                              | ×      | -                                                     |

栈里面存放的一个个实体称为栈帧, 每一个栈帧都包括了局部变量表, 操作数栈, 动态连接, 方法返回地址和一些额外的附加信息, 在编译时, 栈帧中需要多大的局部变量表, 多深的操作数栈都已经完全确定了, 并且写入到方法表的Code属性之中

局部变量表用于存放方法参数和方法内部定义的局部变量, 局部变量表的容量以Slot为最小单位, 一个Slot可以存放一个32位以内的数据类型, long和double需要两个Slot存放.
如果执行的方法是非static方法, 那局部变量表中第0位索引的Slot默认是用于传递方法所属对象实例的引用(this).

方法调用过程:
1. 除非被调用的方法是类方法, 每一次方法调用指令之前, JVM先会把方法被调用的对象引用压入操作数栈中, 除了对象的引用之外, JVM还会把方法的参数依次压入操作数栈.
2. 执行方法调用指令时, JVM会将对象引用和方法参数依次从操作数栈弹出, 新建一个栈帧, 把对象引用和函数参数分别放入新栈帧的局部变量表
3. JVM把新栈帧push入虚拟机方法栈, 将PC寄存器指向方法的第一条指令

可以获取栈中信息:
```Java
Thread.currentThread().getStackTrace()[1].getMethodName();
public StackTraceElement[] getStackTrace();
```

***

## QA

**方法调用时, JVM发生了什么**

创建栈帧, 调用方法时保护现场, 调用方法完毕后恢复现场

## 参考

1. [Java语言层面和JVM层面方法特征签名的区别及实例分析](https://blog.csdn.net/tjiyu/article/details/53891813)
2. [图解JVM执行引擎之方法调用](https://www.cnblogs.com/chenyangyao/p/5305352.html)

